[[algorithms-dag-longeset-path]]
[.alpha]
= Longest Path for DAG
:description: This section describes the Longest Path algorithm for DAGs in the Neo4j Graph Data Science library.
:entity: node
:result: the distance of the longeset path
:algorithm: DAG Longest Path


:directed:
:undirected-unsupported:
:weighted:
include::partial$/algorithms/shared/algorithm-traits.adoc[]
include::partial$/operations-reference/alpha-note.adoc[]


== Introduction

Finding the longest path that leads to a node in a graph is possible to do in linear time for the special case of DAGs, that is graphs which do not contain cycles.

The GDS implementation for this problem is based on topological sort and  operates in linear time.
When the graph is not a DAG, any node that belongs to  component containing at least one cycle will be excluded from the results.
That is, the implementation will only give results for those components of the graph that form DAGs.

You can use xref:algorithms/dag/topological-sort.adoc[topological sort] to make sure the graph is a DAG.

The algorithm supports weighted and unweighted graphs.
Negative weights are currently unsupported.


=== Usage

One example for usage of this algorithm is in the context of a supply chain graph.
If edges indicate the time to supply, then the distance of the longest path to a target node is the time required to manufacture the node from decision to completion.


== Syntax

include::partial$/algorithms/shared/syntax-intro-named-graph.adoc[]

.Longest Path syntax per mode
[.tabbed-example]
====

[.include-with-stream]
======

.Run DAG Longest Path in stream mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.dag.longestPath.stream(
  graphName: String,
  configuration: Map
) YIELD
  index: Integer,
  sourceNode: Integer,
  targetNode: Integer,
  totalCost: Float,
  nodeIds: List of Integer,
  costs: List of Float,
  path: Path
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
| xref:common-usage/running-algos.adoc#common-configuration-relationship-weight-property[relationshipWeightProperty] | String  | null    | yes      | Name of the relationship property to use as weights. If unspecified, the algorithm runs unweighted.
|===

.Results
[opts="header"]
|===
| Name        | Type            | Description
| index       | Integer         | 0-based index of the found path.
| sourceNode  | Integer         | Source node of the path.
| targetNode  | Integer         | Target node of the path.
| totalCost   | Float           | Total cost from source to target.
| nodeIds     | List of Integer | Node ids on the path in traversal order.
| costs       | List of Float   | Accumulated costs for each node on the path.
| path        | Path            | The path represented as Cypher entity.
|===

// include-with-stream
======

// tabbed-example
====


== Examples

:algorithm-name: DAG Longest Path
:graph-description: supply chain
:image-file: longest-path.svg
include::partial$/algorithms/shared/examples-intro.adoc[]

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query]
----
CREATE
       (n0:Goods {name: 'Timber'}),
       (n1:Goods {name: 'Lumber'}),
       (n2:Goods {name: 'Screws'}),
       (n3:Workshop {name: 'Table Maker'}),
       (n4:Product {name: 'Table'}),

       (n0)-[:Processing {time: 1}]->(n1),
       (n1)-[:Shipment {time: 0}]->(n3),
       (n2)-[:Shipment {time: 3}]->(n3),
       (n3)-[:Processing {time: 1}]->(n4)
----

This graph describes a simple supply chain of constructing a table in the Table Maker workshop.
In order to have lumber for the table, the workshop processes timber, which takes 1 day to complete.
Once the lumber is ready, it is already in the workshop, therefor it takes zero time to ship it.
However, the screws take 3 days to be shipped to the workshop.
Only after the workshop has all the requirements met, the table can be constructed, a process that takes 1 day.

The longest path to the table node starts with the screws, then the workshop and then the table, in total: 4 days.
This is the bottleneck path, and total time that takes to manufacture the table.

.The following Cypher statement will project the graph to GDS:
[source, cypher, role=noplay setup-query]
----
MATCH (n)
OPTIONAL MATCH (n)-[r:Processing|Shipment]->(target)
RETURN gds.graph.project("g", n, target, {relationshipProperties: r {.time}})
----


=== Stream

The stream procedure streams every node in the graph and the distance of the longest path that leads to it.

For more details on the stream mode in general, see xref:common-usage/running-algos.adoc#running-algos-stream[Stream].

[role=query-example]
--
.The following will run the Longest Path algorithm in `stream` mode with weights:
[source, cypher, role=noplay setup-query]
----
CALL gds.dag.longestPath.stream("g", {relationshipWeightProperty: "time"})
YIELD index, sourceNode, targetNode, totalCost, nodeIds, costs, path
RETURN
    index,
    gds.util.asNode(sourceNode).name AS sourceNode,
    gds.util.asNode(targetNode).name AS targetNode,
    totalCost,
    [nodeId IN nodeIds | gds.util.asNode(nodeId).name] AS nodeNames,
    costs,
    nodes(path) as path
ORDER BY index
----

We use the utility function `asNode` to return the name of node instead of its ID to make results more readable.

.Results
[opts="header"]
|===
| index | sourceNode | targetNode    | totalCost | nodeNames                    | costs           | path
| 0     | "Timber"   | "Timber"      | 0.0       | ["Timber"]                     | [0.0]           | [Node[0]]
| 1     | "Timber"   | "Lumber"      | 1.0       | ["Timber", "Lumber"]             | [0.0, 1.0]      | [Node[0], Node[1]]
| 2     | "Screws"   | "Table Maker" | 3.0       | ["Screws", "Table Maker"]        | [0.0, 3.0]      | [Node[2], Node[3]]
| 3     | "Screws"   | "Screws"      | 0.0       | ["Screws"]                     | [0.0]           | [Node[2]]
| 4     | "Screws"   | "Table"       | 4.0       | ["Screws", "Table Maker", "Table"] | [0.0, 3.0, 4.0] | [Node[2], Node[3], Node[4]]
|===
--
